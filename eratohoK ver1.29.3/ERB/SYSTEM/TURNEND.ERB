;-------------------------------------------------
;時間経過イベント
;-------------------------------------------------
@EVENTTURNEND
#PRI

FLAG:戦略フェイズページ = 1

;拠点フェイズ
IF TIME == 0
	IF SHOP_TIME >= CALC_SHOP_TIME() || CFLAG:MASTER:捕虜先
		CALL TURNEND_LIFE
	ELSE
		BASE:MASTER:体力 = MAXBASE:MASTER:体力
		BASE:MASTER:気力 = MAXBASE:MASTER:気力
		BEGIN SHOP
	ENDIF
;戦略フェイズ
ELSE
	CALL TURNEND_POLITICS
ENDIF

;逆調教要求日数が残っている場合
IF DIPLOMACY_TRAINED_DAY > 0 && !FLAG:ターンエンド調教 && !FLAG:調教要求フラグ
	DIPLOMACY_TRAINED_DAY --
	;全員の調教参加フラグを解除
	CVARSET CFLAG, 6, 0
	CFLAG:DIPLOMACY_TRAINING_CHARA:調教参加フラグ = 1
	;捕虜逆調教モード
	FLAG:調教モード = 5
	;ウフフモード
	FLAG:ウフフフラグ = 1
	PRINTFORML 作為外交交涉的回報、%ANAME(MASTER)%走向了%ANAME(DIPLOMACY_TRAINING_CHARA)%的寝室……
	CALL START_TRAIN_COMMON
	RETURN
ENDIF

;君主逆調教要求日数が残っている場合
IF DIPLOMACY_TRAINED_DAY < 0 && !FLAG:調教要求フラグ
	DIPLOMACY_TRAINED_DAY ++
	;捕虜逆調教モード
	FLAG:調教モード = 調教_慰安
	FLAG:慰安モード = 3
	FLAG:慰安場所 = 慰安_行き先_貴族の居住地
	;ウフフモード
	FLAG:ウフフフラグ = 1
	FLAG:調教要求フラグ = 1
	FLAG:夜這い = 0
	;アイテムを一通り追加
	ITEM:A_跳蛋 = 1
	ITEM:A_振動棒 = 1
	ITEM:A_肛用振動棒 = 1
	ITEM:A_穿戴式假陰茎 = 1
	ITEM:A_飛機杯 = 1
	ITEM:A_陰蒂夾 = 1
	ITEM:A_乳頭夾 = 1
	ITEM:A_媚薬 = 99
	ITEM:A_排卵誘発剤 = 99
	;全員の調教参加フラグを解除
	CVARSET CFLAG, 6, 0
	CFLAG:DIPLOMACY_TRAINING_CHARA:調教参加フラグ = 1
	CFLAG:DIPLOMACY_TRAINING_CHARA:慰安参加者 = 1
	CFLAG:DIPLOMACY_TRAINING_CHARA:行動済み = 1
	CFLAG:GET_COUNTRY_BOSS(CFLAG:MASTER:所属):調教参加フラグ = 1
	PRINTFORMW 作為外交交涉的回報、%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%走向了%ANAME(DIPLOMACY_TRAINING_CHARA)%的寝室……

	;時間の処理をしないと、「拠点フェイズの終了時にここに到達、調教→調教終了でSLGフェイズのターンエンドが呼ばれ、次期の拠点フェイズに飛ぶ」みたいな挙動になる
	;IF TIME == 0
	;	DAY --
	;	SIF !FLAG:クリアフラグ
	;		TIME = 1
	;ELSEIF TIME == 1
	;	TIME = 0
	;	SHOP_TIME = CALC_SHOP_TIME()
	;ENDIF

	;行動開始時の共通設定
	CALL START_TRAIN_COMMON
	REDRAW 1
	RETURN
ENDIF

$SKIPPED

;デッドエンディングならBEGIN TITLE
IF FLAG:強制エンドフラグ == 1

	SETCOLOR カラー_注意
	PRINTFORMW 我们可以利用当前的数据来创建「周回用存档」
	PRINTFORMW 通过加载周回用存档，可以引继现在的武将和物品，开始新的剧本
	RESETCOLOR

	FLAG:クリア直後フラグ = 1

	CALL SAVE_GAME

	BEGIN TITLE
;ターンエンド調教が立っているなら即時調教モードに入る
ELSEIF FLAG:ターンエンド調教
	SELECTCASE FLAG:ターンエンド調教
		CASE 1
			FLAG:調教モード = 1
		CASE 2
			FLAG:調教モード = 5
			FLAG:ウフフフラグ = 1
		CASE 3
			FLAG:調教モード = 7
			FLAG:慰安モード = 3
	ENDSELECT
	FLAG:ターンエンド調教 = 0
	FLAG:ウフフフラグ = 1
	FLAG:夜這い = 0
	;時間の処理をしないと、「拠点フェイズの終了時にここに到達、調教→調教終了でSLGフェイズのターンエンドが呼ばれ、次期の拠点フェイズに飛ぶ」みたいな挙動になる
	IF TIME == 0
		IF !FLAG:クリアフラグ
			TIME = 1
			DAY --
		ENDIF
	ELSEIF TIME == 1
		TIME = 0
		SHOP_TIME = CALC_SHOP_TIME()
	ENDIF
	FLAG:イベント発生禁止 = 1
	CALL START_TRAIN_COMMON
ELSE
	SIF FLAG:調教要求フラグ
		FLAG:調教要求フラグ = 0
	FLAG:イベント発生禁止 = 0
	BEGIN SHOP
ENDIF

;-------------------------------------------------
;拠点フェイズ終了時の処理
;-------------------------------------------------
@TURNEND_LIFE
SHOP_TIME = 0
SHOP_WORK_COUNT = 0

SIF FLAG:イベント発生禁止
	GOTO SKIPPED

;夜に起こるイベント(夜這いなど)
;CALL EVENT_NIGHT

CALL EVENT_WORK

IF CONFIG:7 == 0
	;捕虜の扱いを設定
	CALL SETSTATE_PRISONER
ELSE
	;捕虜の人数をカウント
	LOCAL:5 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):捕虜先 != 0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):軟禁中 == 0
			LOCAL:5 ++
		ENDIF
	NEXT
	;捕虜が一人でもいればラインを引く
	IF LOCAL:5 >= 1
		CALL SINGLE_DRAWLINE
	ENDIF
ENDIF


CALL EVENT_PRISONER

;捕虜でなければ、FLAG:逆調教回数（捕虜調教回数）を消す
IF !CFLAG:MASTER:捕虜先
	FLAG:逆調教メイン調教者 = 0
	FLAG:逆調教回数 = 0
ENDIF

;口上呼び出し
FOR LOCAL:0, 0, CHARANUM
	;主人公と同一勢力の仕官で、捕虜でない
	IF CFLAG:(LOCAL:0):所属 != 0 && LOCAL:0 != MASTER && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == 0
		CALL KOJO_EVENT(LOCAL:0, 30)
	ENDIF
NEXT

;デイリーイベント
CALL EVENT_DAILY
CALL EVENT_KOJO_DAILY

$SKIPPED


;統一後の場合
IF FLAG:クリアフラグ
	;一期終了時の処理
	SIF !FLAG:ターンエンド調教
		CALL TURNEND_COMMON
	;クールタイムのリセット
	CVARSET COOLTIME, 0
	;ターンを進める
	DAY ++
;捕虜の場合
ELSEIF CFLAG:MASTER:捕虜先 != 0
	;強制的に自動行動
	LOCAL:0 = CONFIG:302
	CONFIG:302 = 1

	CALL SINGLE_DRAWLINE

	;戦略フェイズの処理
	CALL SLG_TURNEND

	;自動行動の設定を元に戻す
	CONFIG:302 = LOCAL:0

	;一期終了時の処理
	CALL TURNEND_COMMON

	;ターンを進める
	DAY ++

ELSE
	;戦略フェイズへ進む
	TIME = 1
ENDIF

;-------------------------------------------------
;戦略フェイズ終了時の処理
;-------------------------------------------------
@TURNEND_POLITICS

SIF FLAG:イベント発生禁止
	GOTO SKIPPED

; 防衛兵と部隊兵を保存
ARRAYCOPY "CITY_SOLDIER", "CITY_SOLDIER_PREV"
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, MAX_UNIT
		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER:(LOCAL:0):(LOCAL:1)
	NEXT
NEXT

;戦闘関係の処理
CALL SLG_TURNEND

;損害を計算
FOR LOCAL:0, 0, MAX_CITY
	CITY_SOLDIER_PREV:(LOCAL:0) = CITY_SOLDIER_PREV:(LOCAL:0) - CITY_SOLDIER:(LOCAL:0)
NEXT
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, MAX_UNIT
		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) - UNIT_SOLDIER:(LOCAL:0):(LOCAL:1)
	NEXT
NEXT

;デッドエンディングなら戻る
IF FLAG:強制エンドフラグ == 1
	RETURN
ENDIF

;一期終了時の処理
SIF !FLAG:ターンエンド調教
	CALL TURNEND_COMMON

$SKIPPED

;時間を進める
DAY ++
IF FLAG:観戦モード
	TIME = 1
ELSE
	TIME = 0
ENDIF

;-------------------------------------------------
;一期終了時の処理
;統一前なら戦略フェイズ終了後、統一後なら拠点フェイズ終了後に呼ばれる
;-------------------------------------------------
@TURNEND_COMMON
VARSET LOCAL, 0
VARSET PRISONER_TARGET, -1

;奴隷市場の商品リストを更新
CALL SLAVEMARKET_PREPARE_GOODS
;スカウトの対象リストを更新
CALL SCOUT_PREPARE_CANDIDATES
;「会いに行く」の対象リストのチェック
CALL MEET_CHECK_CANDIDATES

;このターンの特別訓練実施回数をリセット
;CALC_SPECIAL_TRAIN_PRICEより先にリセットしないと割引が正常に動作しない
SPECIAL_TRAIN_COUNT_THIS_TURN = 0

;特別訓練の価格を更新
;次のターンの特別訓練価格になるので、いったんDAYを1増やした（次のターンの値で）計算し、戻す
DAY ++
CALL CALC_SPECIAL_TRAIN_PRICE
DAY --

;雇用カウンターの操作
PLAYER_HIRED_COUNTER = MAX(0, PLAYER_HIRED_COUNTER - 1)
COUNTRY_PLAYER_BELONGED_COUNTER++
IF COUNTRY_PLAYER_BELONGED_COUNTER == 2 
	ARRAYSHIFT COUNTRY_PLAYER_BELONGED, 1, 0
	COUNTRY_PLAYER_BELONGED_COUNTER = 0
ENDIF

;野盗出現しない期間を1へらす
BANDIT_POP_SUSPENDED = MAX(BANDIT_POP_SUSPENDED - 1, 0)

;自勢力でなくなった動物の削除
FOR LOCAL:0, CHARANUM - 1, -1, -1
	IF IS_ANIMAL(LOCAL:0) && CFLAG:(LOCAL:0):所属 != CFLAG:MASTER:所属
		CALL DELETE_CHARA(LOCAL:0)
	ENDIF
NEXT


ARENA_DONE = 0
APPOINT_DONE = 0

;都市への投資をクリア
VARSET CITY_INVESTED
NOW_INVEST = 0

;捕虜用変数のクリア
CVARSET CFLAG, 30, 0
CVARSET CFLAG, 31, 0
CVARSET CFLAG, 32, 0

;各子育てコマンドの選択回数をクリア
FOR LOCAL:0, 110, 130
	CVARSET CFLAG, LOCAL:0, 0
NEXT

;口上呼び出し
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER
		CALL SINGLE_DRAWLINE()
		CALL KOJO_EVENT(LOCAL:0, 31)
	ENDIF
NEXT

;触手部屋の管理者に経験値を加算
IF ITEM:触手部屋 && ID_TO_CHARA(FLAG:触手部屋管理者) >= 0 && IS_SYOKUSYU_MANAGE(ID_TO_CHARA(FLAG:触手部屋管理者))
	EXP:(ID_TO_CHARA(FLAG:触手部屋管理者)):触手経験値 += 2
	EXP:(ID_TO_CHARA(FLAG:触手部屋管理者)):妖術経験値 += 2
	CALL TRAIN_AUTO_ABLUP(ID_TO_CHARA(FLAG:触手部屋管理者), 1)
ENDIF

CUSTOMDRAWLINE =
PRINTFORML 各種報告
CUSTOMDRAWLINE =

;主人公用の捕虜状態からの脱出イベント呼び出し
CALL MASTER_ESCAPE_FROM_PRISON

IF CFLAG:MASTER:所属 <= 0
	WAIT
;給料の支給(観戦中・捕虜のときはスキップ)◆
ELSEIF CFLAG:MASTER:捕虜先 == 0
	CALL SINGLE_DRAWLINE
	PRINTFORML 給与/徴税報告
	CALL COLOR_LINE
	LOCAL:0 = CALC_SALARY()
	MONEY += LOCAL:0
	PRINTFORML %ANAME(MASTER)%領取了這一期的俸祿
	IF IS_COUNTRY(CFLAG:MASTER:所属)
		PRINTFORML 所持金＋{LOCAL:0}（現在:{MONEY}）
	ELSE
		PRINTFORMW 所持金＋{LOCAL:0}（現在:{MONEY}）
	ENDIF
ENDIF

;各勢力都市の経済規模変動及び税収処理
CALL VARY_ECONOMY
CALL GAIN_COUNTRY_TAX

CALL RESET_CITY_TIRED_COUNT

;経済成長・兵数増強の補正値をクリア
FLAG:経済成長追加値 = 0
FLAG:兵数増加追加値 = 0

;外交による調教要求フラグの解除判定
CALL CHECK_DIPLOMACY_TRAIN_FLAG

;塾による成長
CALL CRAM_SCHOOL

CALL BREWERY

;朝のイベント
;CALL EVENT_MORNING

;アイテム開発イベント
CALL ITEM_DEVELOPMENT

;特殊な朝イベント
;CALL EVENT_MORNING_SP

CALL SINGLE_DRAWLINE
PRINTFORML 来自特殊勢力的調教報告
CALL COLOR_LINE

;特殊勢力による調教
FOR LOCAL, 0, MAX_COUNTRY
	CALL SP_TRAIN(LOCAL)
NEXT

;妊娠出産関連の処理
CALL TURNEND_PREGNANT_PHASE


CALL SINGLE_DRAWLINE
PRINTFORML 脱走・解放
CALL COLOR_LINE


;解放カウンタに基づいて解放
CALL RELEASE_PRISONERS

;捕虜になったときの解放カウンタを加算
;ただし、特殊勢力は自然解放しない
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):捕虜先 && !IS_SP_COUNTRY(CFLAG:(LOCAL:0):捕虜先)
		CFLAG:(LOCAL:0):解放カウンタ ++
	ELSE
		CFLAG:(LOCAL:0):解放カウンタ = 0
	ENDIF
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML 素質獲得報告
CALL COLOR_LINE

FOR LOCAL:0, 0, CHARANUM
	SIF IS_APPEAR(LOCAL:0)
		;恋慕などの素質変化の処理(二回目)
		CALL TALENT_CHECK(LOCAL:0)
NEXT

;キャラクタの能力をどうこうする処理
CALL TURNEND_CHARA_PROCESS

;-------------------------------------------------
;キャラの能力の変動
;-------------------------------------------------
@TURNEND_CHARA_PROCESS
#DIM 対象
#DIM 湯治場カウント, MAX_COUNTRY

VARSET 湯治場カウント

FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL)
		CONTINUE
	湯治場カウント:LOCAL = GET_DEVELOPMENT_COUNT(建造物_湯治場, LOCAL)
	;そこのお前！　大宴会場に含まれる関係改善効果は湯治場6個分だぜ！
	湯治場カウント:LOCAL += GET_DEVELOPMENT_COUNT(建造物_大宴会場, LOCAL) * 6
NEXT


FOR LOCAL:0, 0, CHARANUM

	SIF !IS_APPEAR(LOCAL:0)
		CONTINUE
	;全員の体力・気力を最大まで回復
	BASE:(LOCAL:0):0 = MAXBASE:(LOCAL:0):0
	BASE:(LOCAL:0):1 = MAXBASE:(LOCAL:0):1

	;毛が成長
	CALL INMOU_GROW(LOCAL:0)
	CALL ARMPIT_GROW(LOCAL:0)
	;締まりの改善
	CALL TIGHTNESS_GROW(LOCAL:0)


	IF HAS_TAG(LOCAL, タグ_蓬莱人) && CFLAG:LOCAL:特殊状態 == 特殊状態_死亡
		CALL COLOR_PRINTW("復活！", カラー_注意)
		CALL COLOR_PRINTW(@"%NAME_FORMAL(LOCAL)%蘇生了", カラー_注意)
		CALL CHANGE_COUNTRY(LOCAL, 0, 1)
	ENDIF

	;このターン～系フラグを折る
	CFLAG:(LOCAL:0):行動済み = 0

	;捕虜でなく、所属が特殊勢力でないなら、調教カウンタを減らしていく
	IF !IS_SP_COUNTRY(CFLAG:LOCAL:捕虜先) && !IS_SP_COUNTRY(CFLAG:LOCAL:所属)
		FOR LOCAL:1, 0, VARSIZE("SP_TRAIN_COUNT")
			SP_TRAIN_COUNT:LOCAL:(LOCAL:1) = MAX(SP_TRAIN_COUNT:LOCAL:(LOCAL:1) - 1, 0)
		NEXT
	ENDIF

	IF CFLAG:(LOCAL:0):避妊薬残ターン
		CFLAG:(LOCAL:0):避妊薬残ターン = MAX(CFLAG:(LOCAL:0):避妊薬残ターン - 1, 0)
		IF CFLAG:(LOCAL:0):避妊薬残ターン == 0
			CALL COLOR_PRINTL(@"%ANAME(LOCAL)%的避妊薬失效了", カラー_注意)
			CALL CLEAR_CREAMPIE_HISTORY(LOCAL)
		ENDIF
	ENDIF

	IF CFLAG:(LOCAL:0):排卵誘発剤
		CFLAG:(LOCAL:0):排卵誘発剤 = MAX(CFLAG:(LOCAL:0):排卵誘発剤 - 1, 0)
		SIF CFLAG:(LOCAL:0):排卵誘発剤 == 0
			CALL COLOR_PRINTL(@"%ANAME(LOCAL)%的排卵誘発剤失效了", カラー_注意)
	ENDIF

	;薬物依存値を減らす 薬物中毒あると300以下にならない
	IF GETBIT(TALENT:(LOCAL:0):淫乱系, 素質_淫乱_薬物中毒)
		CFLAG:(LOCAL:0):薬物依存 = MAX(CFLAG:(LOCAL:0):薬物依存 * 9 / 10, 300)
	ELSE
		CFLAG:(LOCAL:0):薬物依存 = MAX(CFLAG:(LOCAL:0):薬物依存 - RAND:(MAX(CFLAG:(LOCAL:0):薬物依存 * 1 / 10, 1)), 0)

	ENDIF

	;特定勢力に所属していて捕虜でない
	IF IS_COUNTRY(CFLAG:(LOCAL:0):所属) && !IS_SP_COUNTRY(CFLAG:(LOCAL:0):所属) && CFLAG:(LOCAL:0):捕虜先 == 0
		FOR LOCAL:1, 0, CHARANUM
			;同一勢力に所属するキャラの間で好感度を上昇させ、敵対度を減少させる
			IF LOCAL:1 != LOCAL:0 && CFLAG:(LOCAL:1):所属 == CFLAG:(LOCAL:0):所属 && CFLAG:(LOCAL:1):捕虜先 == 0

				LOCAL:2 = (1500 - REL_LIKE:(LOCAL:0):(LOCAL:1)) / 50 + TALENT:(LOCAL:1):謎之魅力 * 5 + 湯治場カウント:(CFLAG:(LOCAL:0):所属) * 5
				LOCAL:3 = REL_HATE:(LOCAL:0):(LOCAL:1) / 50 + TALENT:(LOCAL:1):謎之魅力 * 5 + 湯治場カウント:(CFLAG:(LOCAL:0):所属) * 5

				;自然に上がる好感度は1000まで
				IF REL_LIKE:(LOCAL:0):(LOCAL:1) < 1000 + 10 * 湯治場カウント:(CFLAG:(LOCAL:0):所属)
					REL_LIKE:(LOCAL:0):(LOCAL:1) = MIN(REL_LIKE:(LOCAL:0):(LOCAL:1) + LOCAL:2, 1000)
				ENDIF
				REL_HATE:(LOCAL:0):(LOCAL:1) = MAX(REL_HATE:(LOCAL:0):(LOCAL:1) - LOCAL:3, 0)
			ENDIF
		NEXT
	;捕虜である
	ELSEIF CFLAG:(LOCAL:0):捕虜先
		CALL CHANGE_RELATION_O_TO_C(LOCAL:0, CFLAG:(LOCAL:0):捕虜先, 湯治場カウント * 5, 湯治場カウント * -5)
	;放浪中
	ELSEIF CFLAG:LOCAL:特殊状態 == 特殊状態_放浪
		;特殊勢力以外への印象値をフラットにする
		FOR LOCAL:1, 0, MAX_COUNTRY
			IF IS_COUNTRY(LOCAL:1) && !IS_SP_COUNTRY(LOCAL:1)
				LOCAL:2 = GET_COUNTRY_BOSS(LOCAL:1)
				CALL CHANGE_RELATION_O_TO_C(LOCAL, LOCAL:1, REL_LIKE:LOCAL:(LOCAL:2) / 50 * -1, REL_HATE:LOCAL:(LOCAL:2) / 10 * -1)
			ENDIF
		NEXT
	ENDIF

	;捕虜の場合
	IF CFLAG:(LOCAL:0):捕虜先 != 0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
		;捕虜の機嫌変化
		CALL PRISONER_CHANGE_EMOTION(LOCAL:0)

	;被育児状態の子供の場合
	ELSEIF CFLAG:(LOCAL:0):行動不能状態 == 行動不能_子供
		;機嫌は変動しない

	;捕虜でなく、被育児状態の子供でもない場合
	ELSEIF CFLAG:(LOCAL:0):行動不能状態 != 行動不能_子供
		;現在の機嫌を減衰させる
		TIMES PALAM:(LOCAL:0):怒外, 0.60
		TIMES PALAM:(LOCAL:0):哀外, 0.60
		TIMES PALAM:(LOCAL:0):怖外, 0.60
		TIMES PALAM:(LOCAL:0):怒主, 0.05
		TIMES PALAM:(LOCAL:0):哀主, 0.05
		TIMES PALAM:(LOCAL:0):怖主, 0.05

		;●外的要因による機嫌にランダム値を加える
		;<圧抑>持ちは機嫌が変動しない
		IF !TALENT:(LOCAL:0):圧抑
			;怪我をしている場合
			IF CFLAG:(LOCAL:0):行動不能状態 == 3
				IF RAND:2 == 0
					PALAM:(LOCAL:0):怒外 += RAND:1001
				ELSE
					PALAM:(LOCAL:0):怒外 += RAND:301
				ENDIF
				IF RAND:2 == 0
					PALAM:(LOCAL:0):哀外 += RAND:1001
				ELSE
					PALAM:(LOCAL:0):哀外 += RAND:301
				ENDIF

			ELSE
				IF RAND:15 == 0
					PALAM:(LOCAL:0):怒外 += RAND:2001 - 1000
				ELSE
					PALAM:(LOCAL:0):怒外 += RAND:601 - 300
				ENDIF
				IF RAND:15 == 0
					PALAM:(LOCAL:0):哀外 += RAND:2001 - 1000
				ELSE
					PALAM:(LOCAL:0):哀外 += RAND:601 - 300
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;捕虜の収監によるRELATIONの変化
	SIF CFLAG:(LOCAL:0):捕虜先 != 0
		CALL PRISONER_CHANGE_RELATION(LOCAL:0)

	;精神力の回復
	IF CFLAG:(LOCAL:0):精神力回復量補正 == 0
		BASE:(LOCAL:0):精神力 = MIN(BASE:(LOCAL:0):精神力 + MAXBASE:(LOCAL:0):精神力 / 2, MAXBASE:(LOCAL:0):精神力)
	ELSE
		BASE:(LOCAL:0):精神力 = MIN(BASE:(LOCAL:0):精神力 + (MAXBASE:(LOCAL:0):精神力 / 2 * CFLAG:(LOCAL:0):精神力回復量補正 / 100), MAXBASE:(LOCAL:0):精神力)
		CFLAG:(LOCAL:0):精神力回復量補正 = 0
	ENDIF

	;崩壊値の減少
	SIF !TALENT:(LOCAL:0):崩壊
		CFLAG:(LOCAL:0):崩壊 -= 600 + (MAXBASE:(LOCAL:0):精神力 / 5)

	CFLAG:(LOCAL:0):崩壊 = LIMIT(CFLAG:(LOCAL:0):崩壊, 0, 10000)

	;超成長力か努力家があればランダムに能力経験値獲得
	IF FIND_PASSIVE_SKILL(LOCAL:0, "超成長力")|| FIND_PASSIVE_SKILL(LOCAL:0, "努力家")
		LOCAL:1 = RAND(1, 4)
		SELECTCASE IFRAND("0TO5", 1, "6", TALENT:LOCAL:妖術知識)
			CASE 0
				LOCAL:2 = GETNUM(EXP, "武闘経験値")
			CASE 1
				LOCAL:2 = GETNUM(EXP, "防衛経験値")
			CASE 2
				LOCAL:2 = GETNUM(EXP, "知略経験値")
			CASE 3
				LOCAL:2 = GETNUM(EXP, "政治経験値")
			CASE 4
				LOCAL:2 = GETNUM(EXP, "料理経験値")
			CASE 5
				LOCAL:2 = GETNUM(EXP, "歌唱経験値")
			CASE 6
				LOCAL:2 = GETNUM(EXP, "妖術経験値")
				LOCAL:1 *= 20
		ENDSELECT
		EXP:(LOCAL:0):(LOCAL:2) += LOCAL:1
		CALL TRAIN_AUTO_ABLUP(LOCAL:0, 1)
	ENDIF

	;子供の成長度を加算
	SIF TALENT:(LOCAL:0):幼兒
		CFLAG:(LOCAL:0):子の成長度 = MIN(CFLAG:(LOCAL:0):子の成長度 + 1, 999999)

NEXT

;-------------------------------------------------
;「会いに行く」での放浪中キャラの用意
;-------------------------------------------------
@MEET_CHECK_CANDIDATES()

MEET_CANDIDATES_REST_TURN --

FOR LOCAL, 0, MEET_MAX_CANDIDATES
	LOCAL:1 = ID_TO_CHARA(MEET_CANDIDATES:LOCAL)
	SIF LOCAL:1 == -1
		CONTINUE
	IF CFLAG:(LOCAL:1):特殊状態 != 特殊状態_放浪 || CFLAG:(LOCAL:1):捕虜先
		MEET_CANDIDATES:LOCAL = 0
	ENDIF
NEXT

IF !IS_COUNTRY(CFLAG:MASTER:所属) || IS_SP_COUNTRY(CFLAG:MASTER:所属)
	VARSET MEET_CANDIDATES, 0
	VARSET MEET_CANDIDATES_REST_TURN, 0
	RETURN 0
ENDIF

LOCAL:2 = 1

FOR LOCAL, 0, MEET_MAX_CANDIDATES
	LOCAL:1 = ID_TO_CHARA(MEET_CANDIDATES:LOCAL)
	IF LOCAL:1 != -1
		LOCAL:2 = 0
		BREAK
	ENDIF
NEXT

SIF LOCAL:2
	MEET_CANDIDATES_REST_TURN = 0

SIF MEET_CANDIDATES_REST_TURN > 0
	RETURN 0

VARSET MEET_CANDIDATES, 0
VARSET MEET_CANDIDATES_REST_TURN, 0

CALL SELECT_CHARA_RANDOM_MULTI(MEET_MAX_CANDIDATES, "MEET_CHECK_CANDIDATES")

FOR LOCAL, 0, MIN(MEET_MAX_CANDIDATES, SELECTED_CHARA_NUM)
	MEET_CANDIDATES:LOCAL = GET_ID(SELECTED_CHARA:LOCAL)
NEXT

SIF SELECTED_CHARA_NUM > 0
	MEET_CANDIDATES_REST_TURN = 4

@SELECT_CHARA_RANDOM_LOGIC_MEET_CHECK_CANDIDATES(対象)
#DIM 対象
RETURN 対象 != MASTER && !CFLAG:対象:捕虜先 && CFLAG:対象:特殊状態 == 特殊状態_放浪

;-------------------------------------------------
;脱出イベント
;-------------------------------------------------
@MASTER_ESCAPE_FROM_PRISON

SIF !(CFLAG:MASTER:捕虜先 && RAND:100 < 15)
	RETURN -1

CALL SINGLE_DRAWLINE
PRINTFORMW 被囚禁的%ANAME(MASTER)%、発現看守有空隙
PRINTFORMW 現在的話説不定能夠逃走……
CALL ASK_YN("嘗試逃跑", "太危險還是算了吧")

IF RESULT == 1
	PRINTFORMW 失敗的坏会遭受殘酷的対待吧
	PRINTFORMW 現在還是乖乖呆着比較好……
	CALL SINGLE_DRAWLINE
	RETURN
ENDIF


PRINTFORMW 不能錯過這個機会！
PRINTFORMW %ANAME(MASTER)%嘗試着逃跑
PRINTFORML ・
PRINTFORML ・
PRINTFORMW ・

IF RAND:5 <= 3
	IF !CFLAG:MASTER:所属
		PRINTFORMW 成功逃走了！
		PRINTFORMW %ANAME(MASTER)%変成了流浪状態
	ELSE
		PRINTFORMW 成功逃走了！
		PRINTFORMW %ANAME(MASTER)%返回了勢力
	ENDIF
	CALL CAPTURE(MASTER, 0)
	CALL SINGLE_DRAWLINE
	RETURN
ENDIF

IF IS_MALE(MASTER)
	PRINTFORMW 逃跑失敗了……
	PRINTFORMW 被発現的%ANAME(MASTER)%在被拷問之後、丟回了牢房
	RETURN
ENDIF

PRINTFORMW 逃跑失敗了……
PRINTFORMW 被士兵発現的%ANAME(MASTER)%、被帯到其他地方懲罰了
PRINTFORML 
SELECTCASE RAND:5
	CASE 0
		PRINTFORMW %ANAME(MASTER)%被帯到了兵営角落里的飼育場
		PRINTFORML 綁着手脚、以赤裸的状態被丟進了豬群之中
		PRINTFORMW 興奮的豬群迅速囲了過来、直接把陰茎插入了還沒有湿潤的腔道中
		PRINTFORML 因痛苦和厭悪感発出悲鳴的%ANAME(MASTER)%、完全沒有被発情的畜生擔心
		PRINTFORMW 只想讓眼前的雌性確実的懷孕的豬激烈的抽動着、蹂躙着腔肉
		PRINTFORML 打算逃走的%ANAME(MASTER)%拼命的掙扎、但那牢牢插入在最深処的肉棒、讓她的努力毫無意義
		PRINTFORMW 不久之後豬発出了一声高叫、噗嗤噗嗤的把黏黏糊糊的精液全都射在了%ANAME(MASTER)%体内
		PRINTFORMW 那悪心的感触讓%ANAME(MASTER)%流着泪開始求饒、士兵們看着她這個姿態哈哈大笑
		PRINTFORMW 之後%ANAME(MASTER)%は被数不清得到豬一直交配着、身体裡裡外外都沾満了稠糊
	CASE 1
		PRINTFORMW %ANAME(MASTER)%被帯到了男囚的牢獄
		PRINTFORML 監牢中有着大量的犯人。他們看到%ANAME(MASTER)%后　瘋狂的叫喊着　有女人！強姦她！
		PRINTFORMW 看起来明白了什麼的%ANAME(MASTER)%哽咽的求饒、士兵們毫不在意的把%ANAME(MASTER)%扔進了監牢中
		PRINTFORML 昏暗的牢房中、害怕的%ANAME(MASTER)%、從周囲的陰暗処感覚到了無数饑渇的視線
		PRINTFORMW 察覚到危機打算逃走的%ANAME(MASTER)%、被伸出的無数只手摁在了地板上、衣服被粗暴了撕開了
		PRINTFORML 但是尖叫着抵抗的%ANAME(MASTER)%、被強壯的男人團團包囲、全身都被隨心所欲地责弄着
		PRINTFORMW 之後身体格外健壯的男人在被圧住的%ANAME(MASTER)%面前、露出了他兇悪的肉棒
		PRINTFORMW 但是用顫抖的声音説着請停下的%ANAME(MASTER)%、反而在他的心中点燃了加虐之火
		PRINTFORMW 男人們毫不留情的侵犯着%ANAME(MASTER)%、被士兵回收時的%ANAME(MASTER)%已経変得破破爛爛的了
	CASE 2
		PRINTFORMW %ANAME(MASTER)%被帯入了監牢内的拷問間
		PRINTFORMW %ANAME(MASTER)%看到了大量被固定着的拷問器具、耳辺傳来了隔壁房間響起的悲鳴声
		PRINTFORML 打算使用道具的士兵們笑着、把%ANAME(MASTER)%擺在了女性用器具的前面
		PRINTFORMW 一件一件的認真説明着使用方法、想象了会遭受什麼樣的対待的%ANAME(MASTER)%身体顫抖起来
		PRINTFORML 看到她的動作的士兵們奸笑着、説着只要好好的奉仕我們的話就可以放過她
		PRINTFORMW 再次從隔壁房間傳来的悲鳴声、摧毀了%ANAME(MASTER)%的抵抗心、靠了過去開始恭敬的奉仕
		PRINTFORML 得意的士兵們露出的肉棒圧向了%ANAME(MASTER)%、讓%ANAME(MASTER)%跪着用口和手服務着
		PRINTFORMW %ANAME(MASTER)%拼命的奉仕着士兵么、在他們射精之後、発出下流的声音把射在手上和口中的精液全部喝光
		PRINTFORMW 被精液弄得黏黏糊糊的%ANAME(MASTER)%、並沒有平息他們的欲火、之後継續使用着小穴和菊穴奉仕着士兵們
	CASE 3
		PRINTFORMW %ANAME(MASTER)%被剝光之後、只帯着項圈被牽着到処走
		PRINTFORMW  每次感受到其他士兵投来的混合着好奇和情欲的射線、不能遮蓋身体的羞恥讓她満臉通紅
		PRINTFORML 甚至、因為帯着項圈被命令着要四肢着地的爬行
		PRINTFORML 睨みつける%ANAME(MASTER)%に対し、士兵達は剣を突き付け、脱走の罪で処刑されたくなければ大人しく従うように威脅
		PRINTFORMW 説服自己在這種情況下之後老実的服從、%ANAME(MASTER)%顫抖着趴下了
		PRINTFORML 以這個打扮、在兵営内像狗一樣爬行的姿態、屈辱和羞恥讓%ANAME(MASTER)%頭腦沸騰
		PRINTFORMW 不経意間愛液從小穴流下　被士兵指出事実之後、瑟瑟発抖着用干巴巴的話語狡辯着
		PRINTFORML 但是士兵們無視了%ANAME(MASTER)%的話、説着你想要的東西来了、把肉棒插進了%ANAME(MASTER)%的小穴之中
		PRINTFORMW 超出想像的堅挺讓%ANAME(MASTER)%的上半身弾起、発出了荒謬的喘息声
		PRINTFORMW 之後也反復的、像動物一樣交尾被射入了濃厚的精液
	CASE 4
		PRINTFORMW 当做警醒衆人的%ANAME(MASTER)%、被綁在了示衆臺上
		PRINTFORML 拼命掙扎着想要逃脱、但是頭和双手無法從緊縛中解脱
		PRINTFORMW 這個姿勢対士兵們来説、就像是搖着屁股勾引着他們一樣、発出了大声的嘲笑声
		PRINTFORMW 之後其中一個士兵抓着%ANAME(MASTER)%的腰、用肉棒摩擦着被汗液湿潤的小穴
		PRINTFORML 拼命的説服対方放棄的%ANAME(MASTER)%、聽見士兵発誓説已経原諒她了所以不打算侵犯她
		PRINTFORMW %ANAME(MASTER)%、聽見発誓的那一瞬間被插入了！下半身傳来了強烈的衝撃
		PRINTFORML 像被電撃一樣的激烈衝撃讓%ANAME(MASTER)%高声悲鳴。瞬間瞪着眼不知道発生了什麼
		PRINTFORMW 腔道内傳来的感覚讓她馬上明白已経被插入了。士兵完全沒有遵守対%ANAME(MASTER)%説的誓言的打算
		PRINTFORMW 士兵們一辺嘲笑着因為屈辱而流泪的%ANAME(MASTER)%、一辺狠狠地侵犯着%ANAME(MASTER)%
ENDSELECT
CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("士兵"), @"不知道是誰的士兵的\@ RAND:2 ? 肉棒 # 嘴唇\@", "不知道是誰的士兵")
CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("士兵"), @"不知道是誰的士兵的\@ RAND:2 ? 肉棒 # 嘴唇\@", "不知道是誰的士兵")

PRINTFORML 
PRINTFORMW %ANAME(MASTER)%被徹底地凌辱之後、重新被扔進了牢房

RETURN 0


;-------------------------------------------------
;解放カウンタに基づいて捕虜を解放する処理
;-------------------------------------------------
@RELEASE_PRISONERS()
#DIM 特殊捕虜先
VARSET LOCAL

;主人公が捕虜で監禁日数が4日以上ときのみ発生
IF CFLAG:MASTER:捕虜先 && CFLAG:MASTER:解放カウンタ >= 5 && RAND:100 < (CFLAG:MASTER:解放カウンタ - 5) * 15 + 15
	CALL SINGLE_DRAWLINE
	PRINTFORMW 士兵們打開了%ANAME(MASTER)%的牢房、対%ANAME(MASTER)%説道你可以出去了
	PRINTFORMW 看来%ANAME(MASTER)%好像被釋放了…
	PRINTL 
	IF CFLAG:MASTER:所属 == 0
		SETCOLOR カラー_注意
		PRINTFORMW %ANAME(MASTER)%只身在外流浪
		RESETCOLOR
	ELSE
		SETCOLOR カラー_注意
		PRINTFORMW %ANAME(MASTER)%返回了君主身辺
		RESETCOLOR
	ENDIF
	CALL CAPTURE(MASTER, 0)
ENDIF

FOR LOCAL, 1, CHARANUM
	特殊捕虜先 = IS_SP_COUNTRY(CFLAG:LOCAL:捕虜先)
	;もしその国が何らかの理由で滅んでいるのにまだ捕らえられていたら、強制的に解放する
	IF CFLAG:LOCAL:捕虜先 && !IS_COUNTRY(CFLAG:LOCAL:捕虜先)
		IF !LOCAL:1
			CALL SINGLE_DRAWLINE
			LOCAL:1 = 1
		ENDIF
		SETCOLOR カラー_注意
		IF IS_COUNTRY(CFLAG:LOCAL:所属)
			PRINTFORML 俘虜後被釋放的%ANAME(LOCAL)%、回到了%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:所属))%勢力
		ELSE
			PRINTFORML 俘虜後被釋放的%ANAME(LOCAL)%、在外流浪
			CFLAG:LOCAL:特殊状態 = 特殊状態_放浪
		ENDIF
		RESETCOLOR
	;通常の解放処理
	ELSEIF LOCAL != MASTER && CFLAG:LOCAL:捕虜先 && CFLAG:LOCAL:捕虜先 != CFLAG:MASTER:所属 && MIN((CFLAG:LOCAL:解放カウンタ - 5) * 5, 30) > RAND:100 && !特殊捕虜先
		IF !LOCAL:1
			CALL SINGLE_DRAWLINE
			LOCAL:1 = 1
		ENDIF
		SETCOLOR カラー_注意
		IF IS_COUNTRY(CFLAG:LOCAL:所属)
			PRINTFORML 被%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:捕虜先))%俘虜的%ANAME(LOCAL)%\@ CFLAG:LOCAL:解放カウンタ >= 10 ? 好不容易# \@才被釋放了、回到了%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:所属))%勢力
		ELSE
			PRINTFORML 被%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:捕虜先))%俘虜的%ANAME(LOCAL)%被釋放後、流浪在外
		ENDIF
		RESETCOLOR
		CALL CAPTURE(LOCAL:0, 0)
	;たまに脱走する。特殊勢力なら率高め
	ELSEIF (LOCAL != MASTER && CFLAG:LOCAL:捕虜先 && CFLAG:LOCAL:捕虜先 != CFLAG:MASTER:所属 && 特殊捕虜先 * 12 + 3 > RAND:100)
		IF !LOCAL:1
			CALL SINGLE_DRAWLINE
			LOCAL:1 = 1
		ENDIF
		IF CFLAG:LOCAL:捕虜先 != CFLAG:LOCAL:所属 && (!特殊捕虜先 || CFLAG:LOCAL:所属)
			SETCOLOR カラー_注意
			IF IS_COUNTRY(CFLAG:LOCAL:所属)
				PRINTFORML 被%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:捕虜先))%俘虜的%ANAME(LOCAL)%逃走了、回到了%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:所属))%勢力
			ELSE
				PRINTFORML 被%ANAME(GET_COUNTRY_BOSS(CFLAG:LOCAL:捕虜先))%俘虜的%ANAME(LOCAL)%逃走後、流浪在外
			ENDIF
			RESETCOLOR
			CALL CAPTURE(LOCAL:0, 0)
		ENDIF
	ENDIF
NEXT
CALL KILL_EMPTY_LINE()

